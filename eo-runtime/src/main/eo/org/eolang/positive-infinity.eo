# The MIT License (MIT)
#
#  2016-2022 Objectionary.com
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+architect yegor256@gmail.com
+home https://github.com/objectionary/eo
+package org.eolang
+rt jvm org.eolang:eo-runtime:0.0.0
+version 0.0.0

# Positive infinity
[] > positive-infinity
  1.0.div 0.0 > @

  # Tests that $ = x
  [x] > eq
    eq. > @
      ^.as-bytes
      x.as-bytes

  # Tests that $ < x
  [x] > lt
    FALSE > @

  # Tests that $ â‰¤ x
  [x] > lte
    ^.eq x > @

  # Tests that $ > x
  [x] > gt
    not. > @
      or.
        x.as-bytes.eq (nan.as-bytes)
        ^.eq x

  # Tests that $ â‰¥ x
  [x] > gte
    not. > @
      eq.
        x.as-bytes
        nan.as-bytes

  # Multiplication of $ and x
  # @todo #1806:30min. Make positive-infinity.times and negative-infinity.times
  #  worked with multiple arguments without using objects from eo-collections.
  #  See positive-infinity.plus and negative-infinity.plus as an example
  [x...] > times
    x.at 0 > first!

    # here we use the rule "NaN != NaN is TRUE" to check if "num" is NaN
    [num] > check-nan
      not. > @
        num.eq num

    if. > @
      or.
        check-nan first
        first.eq 0.0
        first.eq 0
      nan
      if.
        eq.
          first
          negative-infinity
        first
        ^

  # Sum of $ and x
  [x...] > plus
    negative-infinity.as-bytes > neg-inf-as-bytes!

    [acc index terms check-term] > plus-rec
      if. > @
        index.eq terms.length
        acc
        if.
          check-term
            terms.at index
          nan
          plus-rec
            acc
            index.plus 1
            terms
            check-term

    [term] > check-term
      term > t!
      or. > @
        not.
          t.eq t
        t.as-bytes.eq neg-inf-as-bytes

    if. > @
      eq.
        x.length > terms-count!
        0
      ^
      if.
        terms-count.gt 1
        plus-rec
          ^
          0
          x
          check-term
        if.
          check-term
            x.at 0
          nan
          ^

  # Negation of $
  [] > neg
    negative-infinity > @

  # Difference between $ and x
  # @todo #1806:30min. Make positive-infinity.minus and negative-infinity.minus
  #  worked with multiple arguments without using objects from eo-collections.
  #  See positive-infinity.plus and negative-infinity.plus as an example
  [x...] > minus
    ^.as-bytes > pos-inf-as-bytes!

    [acc index terms check-term] > minus-rec
      if. > @
        index.eq terms.length
        acc
        if.
          check-term
            terms.at index
          nan
          minus-rec
            acc
            index.plus 1
            terms
            check-term

    [term] > check-term
      term > t!
      or. > @
        not.
          t.eq t
        t.as-bytes.eq pos-inf-as-bytes

    if. > @
      eq.
        x.length > terms-count!
        0
      ^
      if.
        terms-count.gt 1
        minus-rec
          ^
          0
          x
          check-term
        if.
          check-term
            x.at 0
          nan
          ^

  # Quotient of the division of $ by x
  # @todo #1806:30min. Make positive-infinity.div and negative-infinity.div
  #  worked with multiple arguments without using objects from eo-collections.
  #  See positive-infinity.plus and negative-infinity.plus as an example
  [x...] > div
    negative-infinity > neg-inf!
    ^ > pos-inf!
    0 > zero!

    # here we use the rule "NaN != NaN is TRUE" to check if "num" is NaN
    [num] > is-nan
      not. > @
        num.eq num

    [term] > is-nan-or-infinite
      or. > @
        is-nan
          term > t!
        t.eq pos-inf
        t.eq neg-inf

    [term] > is-term-gte-zero
      try > @
        []
          term.gte 0 > @
        [e]
          term.gte 0.0 > @
        FALSE

    # @todo #1824:30min. Pass "is-nan-or-infinite" to "div-rec" here and in
    #  negative-infinity. Now for some reason it's not possible to pass two or
    #  more objects-functions to another object. To avoid code duplication
    #  "is-nan-or-infinite" object should be passed to "times-rec" as well as
    #  "is-term-gte-infinite"
    [acc-index-terms infs is-term-gte-zero] > times-rec
      acc-index-terms.at 0 > acc!
      acc-index-terms.at 1 > index!
      acc-index-terms.at 2 > terms
      infs.at 0 > pos-inf
      infs.at 1 > neg-inf

      if. > @
        index.eq terms.length
        acc
        if.
          or.
            not.
              eq.
                terms.at index > term!
                term
            term.eq pos-inf
            term.eq neg-inf
          nan
          []
            if. > @
              acc.gt 0.0
              times-rec
                *
                  if.
                    is-term-gte-zero term > term-gte-zero
                    acc
                    neg-inf
                  index.plus 1 > next-index
                  terms
                * acc neg-inf
                is-term-gte-zero
              times-rec
                *
                  if.
                    term-gte-zero
                    acc
                    pos-inf
                  next-index
                  terms
                * pos-inf acc
                is-term-gte-zero

    if. > @
      eq.
        x.length > terms-count!
        zero
      pos-inf
      if.
        terms-count.gt 1
        times-rec
          * pos-inf zero x
          * pos-inf neg-inf
          is-term-gte-zero
        if.
          is-nan-or-infinite
            x.at zero > first!
          nan
          if.
            is-term-gte-zero first
            pos-inf
            neg-inf
